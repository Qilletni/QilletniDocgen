buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.bit3:jsass:5.10.4'  // Use the latest version available
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.vanniktech.maven.publish' version '0.35.0'
}

group = 'dev.qilletni.docgen'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation ('org.thymeleaf:thymeleaf:3.1.2.RELEASE') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compileOnly 'dev.qilletni.impl:qilletni:1.0.0-SNAPSHOT'

    compileOnly 'dev.qilletni.api:qilletni-api:1.0.0-SNAPSHOT'

    // For markdown parsing
    implementation 'com.vladsch.flexmark:flexmark-all:0.64.8'

    // For caching scanned libraries
    implementation 'org.msgpack:msgpack-core:0.9.10'
    
    // For generating an index
    compileOnly 'com.google.code.gson:gson:2.10.1'

    // SLF4J API dependency
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.22.1'
}

test {
    useJUnitPlatform()
}

mavenPublishing {
    coordinates(group, rootProject.name, version)

    pom {
        name = "Qilletni Docgen"
        description = "Documentation generator for Qilletni"
        inceptionYear = "2024"
        url = "https://docs.qilletni.dev/"
        licenses {
            license {
                name = "MIT"
                url = "https://mit-license.org/"
                distribution = "https://mit-license.org/"
            }
        }
        developers {
            developer {
                id = "RubbaBoy"
                name = "Adam Yarris"
                url = "https://github.com/RubbaBoy/"
            }
        }
        scm {
            url = "https://github.com/Qilletni/QilletniDocgen"
            connection = "scm:git:git://github.com/Qilletni/QilletniDocgen.git"
            developerConnection = "scm:git:ssh://git@github.com/Qilletni/QilletniDocgen.git"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath]
        classpath = files()
    }
}

tasks.withType(Javadoc).configureEach {
    doFirst {
        options.modulePath = classpath.files as List
        classpath = files()
    }
}

tasks.register('generateHtml', JavaExec) {
    mainClass = 'dev.qilletni.docgen.Main'
    classpath = sourceSets.main.runtimeClasspath
}

import io.bit3.jsass.Compiler
import io.bit3.jsass.Options

task compileScss {
    jar.dependsOn compileScss

    doLast {
        File inputFile = file('src/main/resources/static/style.scss')
        File outputFile = file('build/resources/main/static/style.css')
        outputFile.parentFile.mkdirs()

        Compiler compiler = new Compiler()
        Options options = new Options()

        def result = compiler.compileString(inputFile.text, options)
        outputFile.text = result.getCss()
    }
}
